# Author(s) : Loic Touraine

#global defaults
isEmpty(QMAKE_RESMERGER) {
    exists("/Applications/Xcode.app/Contents/Developer/Tools/ResMerger") {
       QMAKE_RESMERGER = "/Applications/Xcode.app/Contents/Developer/Tools/ResMerger"
    } else {
        exists("/Developer/Tools/ResMerger") {
            QMAKE_RESMERGER = "/Developer/Tools/ResMerger"
        } else {
            message("Unable to find ResMerger application : check your Xcode and Developer Tools installation")
        }
    }
}
isEmpty(QMAKE_RESMERGER_FLAGS) {
    QMAKE_RESMERGER_FLAGS = -dstIs DF
}
isEmpty(RESMERGER_DIR):RESMERGER_DIR = .
isEmpty(QMAKE_EXT_RESMERGER):QMAKE_EXT_RESMERGER = .rsrc

bcom_resmerger_source.CONFIG += no_link combine
bcom_resmerger_source.dependency_type = TYPE_C
bcom_resmerger_source.variables = QMAKE_RESMERGER_FLAGS
bcom_resmerger_source.commands = $$QMAKE_RESMERGER $(QMAKE_COMP_QMAKE_RESMERGER_FLAGS) ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
bcom_resmerger_source.output = $$RESMERGER_DIR/$${TARGET}$${first(QMAKE_EXT_RESMERGER)}
bcom_resmerger_source.input = BCOM_REZ_COMPILED_FILES
bcom_resmerger_source.name = RESMERGER $${BCOM_REZ_COMPILED_FILES}
bcom_resmerger_source.variable_out = RESMERGERFILE
silent:bcom_resmerger_source.commands = @echo resmerger$${BCOM_REZ_COMPILED_FILES} && $$wbcom_resmerger_source.commands
QMAKE_EXTRA_COMPILERS += bcom_resmerger_source
